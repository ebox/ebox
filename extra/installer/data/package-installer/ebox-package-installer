#!/usr/bin/perl
# Copyright (C) 2009 eBox Technologies S.L.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License, version 2, as
# published by the Free Software Foundation.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

use warnings;
use strict;

#################
# -- constants --
#################

my $TASK_FILE = 'tasks.list';
my $PACKAGE_FILE = 'packages.list';
my $SELECTION_FILE = 'selection';
my $INSTALL_FILES = '/var/tmp';

my $INSTALLER_TITLE = 'eBox Installer';
my $TASK_TITLE = 'Select eBox tasks to install';
my $PACKAGE_TITLE = 'Select eBox packages to install';
my $IFACES_TITLE = 'Select external interfaces';
my $PASS_TITLE = 'Enter password for eBox interface';
my $PASS_CNFIRM_TITLE = 'Confirm password for eBox interface';
my $MAIL_TITLE = 'Enter default virtual mail domain';
my $FINAL_TITLE = 'Installation finished';
my $FINAL_MESSAGE = 'Now you can access eBox interface at https://localhost/';

my $LOG = ($ENV{LOG} or '/tmp/ebox-installer.log');

my $WHIPTAIL_ARGS = "--clear --backtitle \"$INSTALLER_TITLE\"";

########################
# -- global variables --
########################

my $CNF_password;
my @CNF_selectedIfaces;
my $CNF_domain = undef;

############################
# -- installation process --
############################

my @packages = selectPackagesMenu();
installPackages(@packages);

# Choose password for eBox interface
$CNF_password = askPassword();

# Choose interfaces to mark as external
@CNF_selectedIfaces = selectIfacesMenu();

# Choose mail domain if mail module is installed
if (system ("$INSTALL_FILES/ebox-mail-installed") == 0) {
    $CNF_domain = askMailDomain();
}

preconfiguration();

# Show instalation complete message with access instructions
showDialog($FINAL_TITLE, $FINAL_MESSAGE);

###########################
# -- installer functions --
###########################

sub installPackages # (packages)
{
    my (@packages) = @_;

    my $packageList = join (' ', @packages);

    # Get number of packages to install
    my $num = `LANG=C apt-get install -s $packageList | grep ^Inst | wc -l`;
    chomp($num);

    my $aptCmd = 'LANG=C apt-get install -y --force-yes';
    my $text = 'Installing eBox packages...';
    my $whipCmd = "whiptail $WHIPTAIL_ARGS --gauge \"$text\" 7 64 0";
    my $gaugeCmd = "$INSTALL_FILES/ebox-apt-parse $num | $whipCmd";

    $ENV{DEBIAN_FRONTEND} = 'noninteractive';
    my $ret = system ("$aptCmd $packageList | tee -a $LOG | $gaugeCmd");

    if ($ret != 0) {
        showDialog('Error', 'Installation failed. ' .
                   "Check contents of $LOG to see what happened");
        exit (1);
    }
}

sub selectPackagesMenu # returns array
{
    my @tasks = arrayFromFile($TASK_FILE);
    my @packages = arrayFromFile($PACKAGE_FILE);

    my @selection;
    my $option = '';
    my $ret = 0;
    do {
        unless ($ret == -1) {
            $option = showMenu();
        }
        if ($option eq 'simple') {
            $ret = showChecklist($TASK_TITLE, @tasks);
        } else {
            $ret = showChecklist($PACKAGE_TITLE, @packages);
        }
        if ($ret == 0) {
            @selection = arrayFromFile($SELECTION_FILE);
            unlink ($SELECTION_FILE);
            if (scalar (@selection) == 0) {
                showDialog('Error', 'You must select at least one package.');
                $ret = -1;
            }
        }
    } while ($ret != 0);

    return @selection;
}

sub selectIfacesMenu # returns array
{
    my $ifaceList = `$INSTALL_FILES/ebox-get-ifaces`;
    my @ifaces = split (' ', $ifaceList);

    my @selection;
    my $option = '';
    my $ret = 0;
    do {
        $ret = showChecklist($IFACES_TITLE, @ifaces);
        if ($ret == 0) {
            @selection = arrayFromFile($SELECTION_FILE);
            unlink ($SELECTION_FILE);
        }
    } while ($ret != 0);

    return @selection;
}

sub askPassword # returns string
{
    my $password;
    my $confirm;
    do {
        $password = showPasswordDialog($PASS_TITLE);
        $confirm = showPasswordDialog($PASS_CNFIRM_TITLE);

        if ($password ne $confirm) {
            showDialog('Error', "Passwords don't match");
        }
    } until ($password eq $confirm);

    return $password;
}

sub askMailDomain # returns string
{
    my $domain;
    do {
        $domain = showInputDialog($MAIL_TITLE);

        # TODO: domain validation?
    } until ($domain);

    return $domain;
}

sub preconfiguration
{
    my $text = 'Preconfiguring eBox packages...';
    my $whipCmd = "whiptail $WHIPTAIL_ARGS --gauge \"$text\" 7 64 0";
    my $PROGRESS;
    open ($PROGRESS, '|-', $whipCmd);

    # Enable eBox modules
    system("$INSTALL_FILES/enable-all-modules.pl >> $LOG 2>&1");

    print $PROGRESS "33\n";

    # load LANG variable with default locale
    my @lines = arrayFromFile('/etc/default/locale');
    my $LANG = (split ('"', $lines[0]))[1];
    $ENV{LANG} = $LANG;

    print $PROGRESS "40\n";

    # Append eBox support languages to generate to current supported
    # locales
    my $LOCALES_FILE='/var/lib/locales/supported.d/local';
    my $TMP='/tmp/local.tmp';
    system("cat /var/tmp/locale.gen $LOCALES_FILE > $TMP");
    system("sort $TMP | uniq > $LOCALES_FILE");
    unlink($TMP);

    print $PROGRESS "50\n";

    # Regenerate locales to update the new messages from eBox
    system('/usr/sbin/locale-gen');

    print $PROGRESS "60\n";

    system("/usr/share/ebox/ebox-set-locale $LANG >> $LOG 2>&1");

    print $PROGRESS "70\n";

    # Run ebox-software in order to update packages list (which is done
    # nightly)
    #system('ebox-software');

    # eBox initial setup
    system ("$INSTALL_FILES/ebox-set-password $CNF_password >> $LOG 2>&1");

    print $PROGRESS "80\n";

    foreach my $iface (@CNF_selectedIfaces) {
        system ("$INSTALL_FILES/ebox-set-external $iface >> $LOG 2>&1");
    }

    print $PROGRESS "90\n";

    if (defined $CNF_domain) {
        system ("$INSTALL_FILES/ebox-set-maildomain $CNF_domain >> $LOG 2>&1");
    }

    print $PROGRESS "100\n";

    close ($PROGRESS);
}

#################################
# -- whiptail helper functions --
#################################

sub showMenu
{
    my $title = 'Choose package selection method';

    my $size = 2;
    my $height = $size + 7;
    my $width = 60; # FIXME: find proper size instead of random one

    my $command = "whiptail $WHIPTAIL_ARGS --nocancel --title \"$title\" " .
                  "--menu \"$title\" $height $width $size ";

    $command .= "simple \"Select typical sets of packages\" ";
    $command .= "advanced \"Select packages manually\" ";

    my $file = 'selected_option';
    system ("$command 2> $file");
    my @lines = arrayFromFile($file);
    unlink ($file);

    return $lines[0];
}

sub showChecklist # (title, options)
{
    my ($title, @options) = @_;

    my $size = scalar (@options);
    if ($size > 14) {
        $size = 14;
    }
    my $height = $size + 7;
    my $width = 60; # FIXME: find proper size instead of random one

    my $command = "whiptail $WHIPTAIL_ARGS --separate-output " .
                  "--checklist \"$title\" $height $width $size ";

    foreach my $option (@options) {
        $command .= "$option \"FIXME: Description\" 0 ";
    }

    system ("$command 2> $SELECTION_FILE");
}

sub showInputDialog # (title)
{
    my ($title) = @_;

    showGenericInputDialog('inputbox', $title);
}

sub showPasswordDialog # (title)
{
    my ($title) = @_;

    showGenericInputDialog('passwordbox', $title);
}

sub showGenericInputDialog # (type, title) returns input string
{
    my ($type, $title) = @_;

    my $height = 8;
    my $width = 60; # FIXME: find proper size instead of random one

    my $command = "whiptail $WHIPTAIL_ARGS --separate-output " .
                  "--$type \"$title\" $height $width";

    my @selection;
    do {
        system ("$command 2> $SELECTION_FILE");
        @selection = arrayFromFile($SELECTION_FILE);
        unlink ($SELECTION_FILE);
    } until (scalar(@selection) > 0);

    return $selection[0];
}

sub showDialog # (title, message)
{
    my ($title, $message) = @_;

    my $size = 1;
    my $height = $size + 7;
    my $width = length($message) + 7;

    my $command = "whiptail $WHIPTAIL_ARGS --title \"$title\" " .
                  "--msgbox \"$message\" $height $width";

    system ($command);
}

##############################
# -- other helper functions --
##############################

sub arrayFromFile # (filename)
{
    my ($filename) = @_;

    my $fh;
    open ($fh, '<', "$INSTALL_FILES/$filename") or
        open ($fh, '<', $filename) or
            die "Can't read $filename file";
    my @array = <$fh>;
    chomp (@array);
    close ($fh);

    return @array;
}

