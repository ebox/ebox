#!/usr/bin/perl
use strict;
use warnings;

use locale;

use EBox;
use EBox::Config;
use EBox::Gettext;
use EBox::Global;
use EBox::Menu::Root;
use EBox::CGI::Run;
use Storable qw(store);

use Error qw(:try);

EBox::init();

my %keywords = ();

sub add_word
{
    my ($word, $id) = @_;
    if(not defined($keywords{$word})) {
        $keywords{$word} = [];
    }
    if(!grep(/^$id$/, @{$keywords{$word}})) {
        push(@{$keywords{$word}}, $id);
    }
}

sub get_keywords
{
    my ($item) = @_;
    if(defined($item->{'text'})) {
        my $text = $item->{'text'};
        $text = lc($text);
        my @words = split('\W+', $text);
        for my $word (@words) {
            add_word($word, $item->{id});
        }
    }
    if(defined($item->{'url'})) {
        try {
            my $classname = EBox::CGI::Run::classFromUrl($item->{'url'});
            my ($model, $action) = EBox::CGI::Run::lookupModel($classname);
            if($model) {
                my $words = $model->keywords();
                for my $word (@{$words}) {
                    add_word($word, $item->{id});
                }
            }
        } otherwise {
            print 'No model found for ' . $item->{'url'} . "\n";
        }
    }
    if($item->items()) {
        for my $i (@{$item->items()}) {
            get_keywords($i);
        }
    }
}

my $root = new EBox::Menu::Root();
my $domain = gettextdomain();

my $global = EBox::Global->getInstance();
foreach (@{$global->modNames}) {
    my $mod = $global->modInstance($_);
    settextdomain($mod->domain);
    $mod->menu($root);
}

get_keywords($root);

my $file = EBox::Config::tmp . "menucache";
store(\%keywords, $file);
